group = 'io.swagger'
version  = '1.0.0'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def nexusUrl = localProperties.getProperty('nexus.url')
def nexusUser = localProperties.getProperty('nexus.user')
def nexusPwd = localProperties.getProperty('nexus.password')

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}


apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Rename the aar correctly
    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "${project.name}-${variant.baseName}-${version}.aar"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}


ext {
    swagger_annotations_version = "1.5.0"
    gson_version = "2.3.1"
    volley_version = "1.1.0"
    junit_version = "4.12"
    robolectric_version = "3.0"
    concurrent_unit_version = "0.4.2"
}

dependencies {
    api "io.swagger:swagger-annotations:$swagger_annotations_version"
    api "com.google.code.gson:gson:$gson_version"
    api "com.android.volley:volley:${volley_version}"
    testCompile "junit:junit:$junit_version"
    testCompile "org.robolectric:robolectric:${robolectric_version}"
    testCompile "net.jodah:concurrentunit:${concurrent_unit_version}"
    api group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.3.1'
}

apply plugin: 'digital.wup.android-maven-publish'

publishing  {
    publications {
        mavenAar(MavenPublication) {
            from components.android
        }
    }

    repositories {
        maven {
            name = 'nexus'
            url = nexusUrl
            credentials {
                username = nexusUser
                password = nexusPwd
            }
        }
    }
}
